FUNCTION F(X)
  IMPLICIT NONE
  DOUBLE PRECISION, INTENT (in)  :: X
  DOUBLE PRECISION :: F

  F = X**3 * EXP(-X)
END FUNCTION F

!     D FINT(X) / D X = F
FUNCTION FINT(X)
  IMPLICIT NONE

  DOUBLE PRECISION, INTENT (in) :: X
  DOUBLE PRECISION :: FINT

  FINT = -(6.0D0 + 6.0D0 * X + 3.0D0 * X**2 + X**3) * EXP(-X)
END FUNCTION FINT


PROGRAM gauss_integration
  IMPLICIT NONE

  DOUBLE PRECISION, PARAMETER :: A = 2.1d0, B = 5.2d0

  INTERFACE
     FUNCTION F(X)
       IMPLICIT NONE
       DOUBLE PRECISION, INTENT (in)  :: X
       DOUBLE PRECISION :: F
     END FUNCTION F

     FUNCTION FINT(X)
       IMPLICIT NONE

       DOUBLE PRECISION, INTENT (in) :: X
       DOUBLE PRECISION :: FINT
     END FUNCTION FINT

     FUNCTION GAUSS2(A, B, F)
       IMPLICIT NONE

       DOUBLE PRECISION, INTENT (in) :: A, B
       DOUBLE PRECISION :: GAUSS2

       INTERFACE
          FUNCTION F(X)
            IMPLICIT NONE
            DOUBLE PRECISION, INTENT (in)  :: X
            DOUBLE PRECISION :: F
          END FUNCTION F
       END INTERFACE
     END FUNCTION GAUSS2

     FUNCTION GAUSS3(A, B, F)
       IMPLICIT NONE

       DOUBLE PRECISION, INTENT (in) :: A, B
       DOUBLE PRECISION :: GAUSS3

       INTERFACE
          FUNCTION F(X)
            IMPLICIT NONE
            DOUBLE PRECISION, INTENT (in)  :: X
            DOUBLE PRECISION :: F
          END FUNCTION F
       END INTERFACE
     END FUNCTION GAUSS3

  END INTERFACE


  PRINT *, "Gauss 2 points:", GAUSS2(A, B, F)
  PRINT *, "Gauss 3 points:", GAUSS3(A, B, F)
  PRINT *, "correct: ", FINT(B) - FINT(A)
END PROGRAM gauss_integration


FUNCTION GAUSS2(A, B, F)
  IMPLICIT NONE

  DOUBLE PRECISION, INTENT (in) :: A, B
  DOUBLE PRECISION :: GAUSS2

  INTERFACE
     FUNCTION F(X)
       IMPLICIT NONE
       DOUBLE PRECISION, INTENT (in)  :: X
       DOUBLE PRECISION :: F
     END FUNCTION F
  END INTERFACE

  DOUBLE PRECISION :: X(2), C(2), SUM, Y
  INTEGER :: I

  C(1) = 1.0D0
  C(2) = 1.0D0

  X(1) = -1.0D0 / SQRT(3.0D0)
  X(2) =  1.0D0 / SQRT(3.0D0)

  SUM = 0.0D0
  DO I=1,2
     Y = ( (B-A) * X(I) + (B+A) ) / 2.0D0
     SUM = SUM + C(I) * F(Y)
  ENDDO

  GAUSS2 = SUM * (B-A) / 2.0D0

END FUNCTION GAUSS2

FUNCTION GAUSS3(A, B, F)
  IMPLICIT NONE

  DOUBLE PRECISION, INTENT (in) :: A, B
  DOUBLE PRECISION :: GAUSS3

  INTERFACE
     FUNCTION F(X)
       IMPLICIT NONE
       DOUBLE PRECISION, INTENT (in)  :: X
       DOUBLE PRECISION :: F
     END FUNCTION F
  END INTERFACE

  DOUBLE PRECISION :: X(3), C(3), SUM, Y
  INTEGER :: I

  C(1) = 5.0D0 / 9.0D0
  C(2) = 8.0D0 / 9.0D0
  C(3) = 5.0D0 / 9.0D0

  X(1) = -SQRT(0.6D0)
  X(2) = 0.0D0
  X(3) =  SQRT(0.6D0)

  SUM = 0.0D0
  DO I=1,3
     Y = ( (B-A) * X(I) + (B+A) ) / 2.0D0
     SUM = SUM + C(I) * F(Y)
  ENDDO

  GAUSS3 = SUM * (B-A) / 2.0D0

END FUNCTION GAUSS3
